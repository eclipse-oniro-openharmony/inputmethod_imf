# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/inputmethod/imf/inputmethod.gni")
import("//build/ohos.gni")

config("inputmethod_tdd_util_config") {
  visibility = [ ":*" ]
  include_dirs = [ "./" ]
  ldflags = [ "-Wl,--exclude-libs=ALL" ]
  cflags = [
    "-fdata-sections",
    "-ffunction-sections",
    "-fvisibility=hidden",
  ]
}

config("inputmethod_tdd_util_public_config") {
  visibility = [ "./*" ]
  include_dirs = [
    "include",
    "${bundllemanager_path}/interfaces/inner_api/appexecfwk_core/include/bundlemgr",
    "${inputmethod_path}/services/adapter/settings_data_provider/common/include",
  ]
}

ohos_static_library("inputmethod_tdd_util") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sources = [
    "src/key_event_util.cpp",
    "src/tdd_util.cpp",
  ]
  configs = [ ":inputmethod_tdd_util_config" ]
  public_configs = [ ":inputmethod_tdd_util_public_config" ]
  deps = [
    "${inputmethod_path}/services:inputmethod_service",
    "${inputmethod_path}/services/adapter/settings_data_provider:settings_data_static",
  ]
  external_deps = [
    "access_token:libaccesstoken_sdk",
    "access_token:libnativetoken",
    "access_token:libtoken_setproc",
    "bundle_framework:appexecfwk_core",
    "cJSON:cjson",
    "data_share:datashare_common",
    "data_share:datashare_consumer",
    "hilog:libhilog",
    "input:libmmi-client",
    "ipc:ipc_single",
    "os_account:os_account_innerkits",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:libdm",
  ]

  if (window_manager_use_sceneboard) {
    external_deps += [ "window_manager:libwm_lite" ]
  } else {
    external_deps += [ "window_manager:libwm" ]
  }

  subsystem_name = "inputmethod"
  part_name = "imf"
}
